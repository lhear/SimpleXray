plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.compose'
    id 'com.google.protobuf' version '0.9.4'
    id 'com.google.devtools.ksp'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.25.1'
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.74.0'
        }
        grpc_kotlin {
            artifact = 'io.grpc:protoc-gen-grpc-kotlin:1.4.3:jdk8'
        }
        grpckt {
            artifact = 'io.grpc:protoc-gen-grpc-kotlin:1.5.0:jdk8@jar'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {}
            }
            task.plugins {
                grpc {}
                grpckt {}
            }
        }
    }
}

def versionProps = new Properties()
file("$rootDir/version.properties").withInputStream { versionProps.load(it) }

android {
    namespace "com.simplexray.an"
    compileSdk 35

    defaultConfig {
        applicationId "com.simplexray.an"
        versionCode versionProps.APP_VERSION_CODE.toInteger()
        versionName versionProps.APP_VERSION_NAME
        ndk {
            abiFilters "arm64-v8a", "armeabi-v7a", "x86", "x86_64"
        }
        externalNativeBuild {
            ndkBuild {
                arguments "APP_CFLAGS+=-DPKGNAME=com/simplexray/an/service -ffile-prefix-map=${rootDir}=."
                arguments "APP_LDFLAGS+=-Wl,--build-id=none"
            }
        }
        targetSdk 35
        minSdk 29

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // BuildConfig fields for update mechanism
        buildConfigField "String", "REPOSITORY_URL", "\"https://github.com/halibiram/SimpleXray\""
        buildConfigField "String[]", "SUPPORTED_ABIS", "new String[]{\"arm64-v8a\", \"x86_64\"}"
    }

    signingConfigs {
        release {
            enableV1Signing true
            enableV2Signing true
            enableV3Signing true
            enableV4Signing false
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.release
            proguardFiles(
                    getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard-rules.pro'
            )
        }
        debug {
            minifyEnabled false
            signingConfig signingConfigs.release
            debuggable true
        }
    }

    // COMPAT: JavaVersion.VERSION_21 is required for Android SDK 35
    // Verified: Java 21 is fully compatible with Android SDK 35 and required for modern Android development
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
    }

    def propsFile = rootProject.file('store.properties')
    def configName = 'release'

    if (propsFile.exists() && signingConfigs.hasProperty(configName)) {
        def props = new Properties()
        props.load(new FileInputStream(propsFile))
        if (props != null && props.containsKey('storeFile')) {
            signingConfigs.named(configName).configure {
                storeFile = rootProject.file(props['storeFile'])
                storePassword = props['storePassword']
                keyAlias = props['keyAlias']
                keyPassword = props['keyPassword']
            }
        }
    }

    externalNativeBuild {
        ndkBuild {
            path "src/main/jni/Android.mk"
        }
    }

    lint {
        // Enable lint checks for release builds but don't abort on errors
        // This allows builds to complete while still reporting issues
        checkReleaseBuilds = true
        // Continue the build even when errors are found (warnings only)
        abortOnError = false
        // Treat warnings as errors only in strict mode (not enabled by default)
        warningsAsErrors = false
    }

    packagingOptions {
        jniLibs {
            useLegacyPackaging true
        }
    }

    dependenciesInfo {
        // Disables dependency metadata when building APKs.
        includeInApk = false
        // Disables dependency metadata when building Android App Bundles.
        includeInBundle = false
    }
    ndkVersion versionProps.NDK_VERSION

    splits {
        abi {
            enable true
            reset()
            include "arm64-v8a", "armeabi-v7a", "x86", "x86_64"
            universalApk true
        }
    }

    applicationVariants.configureEach { variant ->
        variant.outputs.each { output ->
            def abiFilter = output.getFilter("ABI")
            if (abiFilter != null) {
                output.outputFileName = "simplexray-${abiFilter}.apk"
            } else {
                output.outputFileName = "simplexray-universal.apk"
            }
        }
    }

    buildFeatures {
        compose true
        buildConfig true
    }

    // COMPAT: jvmToolchain(21) requires JDK 21 - required for building this project
    // Note: JDK 21 is required for Android SDK 35 compatibility. Ensure JDK 21 is installed before building.
    kotlin {
        jvmToolchain(21)
    }

    sourceSets {
        main {
            java {
                srcDirs 'src/main/java', 'build/generated/source/proto/main/java'
            }
            kotlin {
                srcDirs 'src/main/kotlin', 'build/generated/source/proto/main/grpckt', 'build/generated/ksp/debug/kotlin', 'build/generated/ksp/release/kotlin'
            }
        }
    }
}

dependencies {
    implementation "io.grpc:grpc-okhttp:1.74.0"
    implementation "io.grpc:grpc-protobuf:1.74.0"
    implementation "io.grpc:grpc-stub:1.74.0"
    implementation "io.grpc:grpc-kotlin-stub:1.4.3"

    implementation 'com.google.android.material:material:1.12.0'
    implementation 'com.google.code.gson:gson:2.13.1'
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation 'androidx.core:core-ktx:1.16.0'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    
    // Firebase
    implementation platform('com.google.firebase:firebase-bom:33.7.0')
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'
    
    // Dependency Injection
    implementation "javax.inject:javax.inject:1"

    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.9.0"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.9.0"

    implementation platform('androidx.compose:compose-bom:2025.08.00')
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.ui:ui-graphics'
    implementation 'androidx.compose.material3:material3'
    implementation 'androidx.compose.material:material-icons-extended'
    implementation 'androidx.compose.ui:ui-tooling-preview-android'
    implementation 'androidx.activity:activity-compose'
    implementation 'androidx.compose.runtime:runtime-livedata'
    implementation 'androidx.navigation:navigation-compose-android:2.9.3'

    implementation 'com.github.nanihadesuka:LazyColumnScrollbar:2.2.0'
    implementation 'sh.calvin.reorderable:reorderable:2.5.1'
    implementation "com.squareup.okhttp3:okhttp:4.12.0"

    // Room Database
    def room_version = "2.6.1"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    ksp "androidx.room:room-compiler:$room_version"

    // WorkManager
    implementation "androidx.work:work-runtime-ktx:2.9.0"

    // Vico Charts
    implementation "com.patrykandpatrick.vico:compose:2.0.0-alpha.28"
    implementation "com.patrykandpatrick.vico:compose-m3:2.0.0-alpha.28"
    implementation "com.patrykandpatrick.vico:core:2.0.0-alpha.28"

    // Glance for Widgets
    implementation "androidx.glance:glance:1.1.0"
    implementation "androidx.glance:glance-appwidget:1.1.0"
    implementation "androidx.glance:glance-material3:1.1.0"

    // ViewModel
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:2.8.7"
    implementation "androidx.lifecycle:lifecycle-runtime-compose:2.8.7"

    // Testing dependencies
    // Note: JUnit 4 is used for Android compatibility. JUnit 5 (Jupiter) requires additional setup.
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'io.mockk:mockk:1.13.8'
    // Updated to match coroutines version for better compatibility
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.9.0'
    testImplementation 'androidx.arch.core:core-testing:2.2.0'
    testImplementation 'com.google.truth:truth:1.1.5'
    testImplementation 'org.json:json:20240303'

    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation 'androidx.test:core:1.5.0'
    androidTestImplementation platform('androidx.compose:compose-bom:2025.08.00')
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4'
    androidTestImplementation 'io.mockk:mockk-android:1.13.8'

    debugImplementation 'androidx.compose.ui:ui-tooling'
    debugImplementation 'androidx.compose.ui:ui-test-manifest'
}
